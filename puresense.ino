// =========================
// Bibliotecas
// =========================
#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <PubSubClient.h>
#include <Adafruit_GFX.h>
#include <Adafruit_ST7735.h>
#include <SPI.h>
#include "DHT.h"
#include <Preferences.h>
#include <ArduinoJson.h>

// ====== Config Wi-Fi ======
const char* ssid = "Movimento sem net";
const char* password = "";

// ====== Config AWS ======
const char* aws_endpoint = "";
const int port = 8883;
const char* mqtt_topic = "puresense/sensores";

// ====== Certificados AWS (exemplo em texto) ======
// TODO: Use arquivos externos (.pem) para seguran√ßa real.

// Certificado da Amazon (Root CA 1)
static const char ca_cert[];

// Certificado do Dispositivo
static const char client_cert[];

// Chave Privada do Dispositivo
static const char private_key[];
// >>>>>>>>>> FIM DA √ÅREA DE DADOS <<<<<<<<<<

// =========================
// Defini√ß√£o dos pinos
// =========================
// Display
#define TFT_CS    5
#define TFT_RST   15
#define TFT_DC    2

#define TFT_SCLK  18
#define TFT_MOSI  23

// DHT11
#define DHTPIN 4
#define DHTTYPE DHT11

// MQ-7 e MQ-2
#define MQ7_PIN 34  // pino anal√≥gico
#define MQ2_PIN 35  // pino anal√≥gico

// =========================
// Inicializa√ß√µes
// =========================
DHT dht(DHTPIN, DHTTYPE);
Adafruit_ST7735 tft = Adafruit_ST7735(TFT_CS, TFT_DC, TFT_RST);

Preferences prefs;

// ====== Objetos Wi-Fi e MQTT ======
WiFiClientSecure net;
PubSubClient client(net);

// Resist√™ncia de carga (verificar o m√≥dulo, geralmente √© 10K)
const float RL_MQ7 = 10.0;
const float RL_MQ2 = 10.0;

// Valores de calibra√ß√£o
float R0_MQ7 = 0;
float R0_MQ2 = 0;

// =========================
// Setup
// =========================
void setup() {
  Serial.begin(115200);
  connectWiFi();
  synchTime();
  connectAWS();

  // Inicia sensores
  dht.begin();

  // Inicia Display
  tft.initR(INITR_BLACKTAB);
  tft.setRotation(1);
  tft.fillScreen(ST77XX_BLACK);
  tft.setTextColor(ST77XX_WHITE);
  tft.setTextSize(1);
  tft.setCursor(10, 10);
  tft.println("PureSense");
  delay(2000);

  // Calibrar sensores MQ
  calibrarMQ();
}

// =========================
// Loop principal
// =========================
void loop() {
  if (!client.connected()) connectAWS();
  client.loop();

  // Leitura DHT11
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();

  if (isnan(temperature) || isnan(humidity)) {
    Serial.println("Erro na leitura do DHT11");
  }

  // Leitura MQ-7
  float adc_mq7 = analogRead(MQ7_PIN);
  float volt_mq7 = adc_mq7 * (3.3 / 4095.0);
  float Rs_mq7 = (3.3 - volt_mq7) * RL_MQ7 / volt_mq7;
  float ratio_mq7 = Rs_mq7 / R0_MQ7;
  float co_ppm_mq7 = pow((ratio_mq7 / 99.042), (1.0 / -1.518)); // Exemplo curva CO

  // Leitura MQ-2
  float adc_mq2 = analogRead(MQ2_PIN);
  float volt_mq2 = adc_mq2 * (3.3 / 4095.0);
  float Rs_mq2 = (3.3 - volt_mq2) * RL_MQ2 / volt_mq2;
  float ratio_mq2 = Rs_mq2 / R0_MQ2;
  float gases_ppm_mq2 = pow((ratio_mq2 / 9.8), (1.0 / -2.0)); // Curva gen√©rica fuma√ßa

  // Debug Serial
  Serial.println("====== Leitura ======");
  Serial.print("Temperatura: "); Serial.print(temperature); Serial.println(" C");
  Serial.print("Umidade: "); Serial.print(humidity); Serial.println(" %");
  Serial.print("CO (MQ-7): "); Serial.print(co_ppm_mq7); Serial.println(" ppm");
  Serial.print("Gases (MQ-2): "); Serial.print(gases_ppm_mq2); Serial.println(" ppm");
  Serial.println("=====================");

  // Display
  atualizarDisplay(temperature, humidity, co_ppm_mq7, gases_ppm_mq2);

  // AWS
  sendToAWS(temperature, humidity, co_ppm_mq7, gases_ppm_mq2);

  delay(300000); // 5 minutos entre leituras
}

// =========================
// Fun√ß√£o para calibra√ß√£o
// =========================
void calibrarMQ() {
  prefs.begin("calibracao", false);

  R0_MQ7 = prefs.getFloat("R0_MQ7", 0);
  R0_MQ2 = prefs.getFloat("R0_MQ2", 0);

  if (R0_MQ7 == 0 || R0_MQ2 == 0) {
    Serial.println("‚öôÔ∏è Calibrando sensores MQ...");
    tft.fillScreen(ST77XX_BLACK);
    tft.setCursor(5, 30);
    tft.println("Calibrando MQ...");
    delay(5000);  // Estabiliza

    // MQ-7
    float adc7 = analogRead(MQ7_PIN);
    float volt7 = adc7 * (3.3 / 4095.0);
    float Rs7 = (3.3 - volt7) * RL_MQ7 / volt7;
    R0_MQ7 = Rs7;

    // MQ-2
    float adc2 = analogRead(MQ2_PIN);
    float volt2 = adc2 * (3.3 / 4095.0);
    float Rs2 = (3.3 - volt2) * RL_MQ2 / volt2;
    R0_MQ2 = Rs2;

    // Salvar na flash
    prefs.putFloat("R0_MQ7", R0_MQ7);
    prefs.putFloat("R0_MQ2", R0_MQ2);

    Serial.print("R0 MQ-7 calibrado: "); Serial.println(R0_MQ7);
    Serial.print("R0 MQ-2 calibrado: "); Serial.println(R0_MQ2);
  } else {
    Serial.println("üì¶ Valores de R0 carregados da memoria.");
    Serial.print("R0 MQ-7: "); Serial.println(R0_MQ7);
    Serial.print("R0 MQ-2: "); Serial.println(R0_MQ2);
  }

  prefs.end();
}

// =========================
// Atualizar Display
// =========================
void atualizarDisplay(float temperature, float humidity, float co_ppm_mq7, float gases_ppm_mq2) {
  tft.fillScreen(ST77XX_BLACK);

  tft.setCursor(5, 5);
  tft.setTextColor(ST77XX_WHITE);
  tft.setTextSize(1);
  tft.println("PureSense Monitor");

  tft.setCursor(5, 25);
  tft.print("Temp: "); tft.print(temperature); tft.println(" C");

  tft.setCursor(5, 40);
  tft.print("Umid: "); tft.print(humidity); tft.println(" %");

  tft.setCursor(5, 55);
  tft.print("CO: "); tft.print(co_ppm_mq7); tft.println(" ppm");

  tft.setCursor(5, 70);
  tft.print("Gases: "); tft.print(gases_ppm_mq2); tft.println(" ppm");
}

void connectWiFi() {
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao Wi-Fi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500); Serial.print(".");
  }
  Serial.println("\n‚úÖ Wi-Fi conectado.");
}

void connectAWS() {
  net.setCACert(ca_cert);
  net.setCertificate(client_cert);
  net.setPrivateKey(private_key);
  client.setServer(aws_endpoint, port);
  
  // Gera o Client ID a partir do MAC Address
  String clientId = "ESP32_PureSense-" + getMacAddress();
  Serial.print("Tentando conectar com o Client ID: ");
  Serial.println(clientId);

  while (!client.connected()) {
    Serial.print("Conectando √† AWS IoT...");
    if (client.connect(clientId.c_str())) {
      Serial.println("‚úÖ Conectado!");
    } else {
      Serial.print("Falha: ");
      Serial.println(client.state());
      Serial.println("Tentando novamente em 2 segundos...");
      delay(2000);
    }
  }
}

void sendToAWS(float temperature, float humidity, float co_ppm_mq7, float gases_ppm_mq2) {
  // String payload = "{";
  // payload += "\"temperature\":" + String(temperature, 1) + ",";
  // payload += "\"humidity\":" + String(humidity, 1) + ",";
  // payload += "\"co\":" + String(co_ppm_mq7, 2) + ",";
  // payload += "\"gases\":" + String(gases_ppm_mq2, 2);
  // payload += "}";

  // client.publish(mqtt_topic, payload.c_str());
  // Serial.println("üì§ Enviado para AWS IoT:");
  // Serial.println(payload);
  StaticJsonDocument<200> doc; // Crie um documento JSON

  // Adiciona o ID √∫nico ao JSON
  doc["deviceId"] = getMacAddress();
  // Adicione os dados dos sensores
  doc["temperature"] = temperature;
  doc["humidity"] = humidity;
  doc["co"] = co_ppm_mq7;
  doc["gases"] = gases_ppm_mq2;

  char jsonBuffer[512];
  serializeJson(doc, jsonBuffer); // Converte o JSON para uma string

  client.publish(mqtt_topic, jsonBuffer); // Publica a string
  Serial.println("üì§ Enviado para AWS IoT:");
  Serial.println(jsonBuffer);
}

void synchTime() {
  const char* ntpServer = "pool.ntp.org";
  const long  gmtOffset_sec = -10800; // Offset para Hor√°rio de Bras√≠lia (GMT-3)
  const int   daylightOffset_sec = 0;   // Sem hor√°rio de ver√£o

  Serial.print("Sincronizando a hora...");
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
  
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) {
    Serial.println(" ‚ùå Falha ao obter a hora. Reiniciando...");
    delay(1000);
    ESP.restart();
  }
  Serial.println("\n‚úÖ Hora sincronizada!");
}

String getMacAddress() {
  String mac = WiFi.macAddress();
  mac.replace(":", ""); // Remove os dois-pontos
  return mac;
}
